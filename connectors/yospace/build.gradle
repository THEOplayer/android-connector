import org.jetbrains.dokka.gradle.AbstractDokkaLeafTask

plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlinx.serialization
    alias libs.plugins.dokka
}
apply from: "$rootDir/connectors/publish.gradle"

android {
    namespace 'com.theoplayer.android.connector.yospace'
    compileSdk 35

    defaultConfig {
        minSdk 21
        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "LIBRARY_VERSION", "\"${sdkVersion}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    compileOnly "com.theoplayer.theoplayer-sdk-android:core:$sdkVersion"
    compileOnly("com.yospace:admanagement-sdk") {
        version {
            strictly("[3.6, 4.0)")
            prefer(yospaceVersion)
        }
    }
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3"

    // Tests
    testImplementation "androidx.test:core:1.6.1"
    testImplementation "androidx.test.ext:junit:1.2.1"
    testImplementation "com.theoplayer.theoplayer-sdk-android:core:$sdkVersion"
    testImplementation "com.yospace:admanagement-sdk:$yospaceVersion"
}

tasks.withType(AbstractDokkaLeafTask.class).configureEach {
    moduleName = "Yospace Connector"

    dokkaSourceSets.named("main") {
        samples.from(project.fileTree("src/test/java/"))
    }
}
