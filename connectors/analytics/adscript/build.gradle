import java.util.zip.GZIPInputStream

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace 'com.theoplayer.android.connector.analytics.adscript'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

// The adscript SDK location should be set by the app and passed in a adscriptSdkDir property.
def aarFileName = 'AdScriptApiClient_v1.0.10.aar'
def adscriptSdkDir = rootProject.properties['adscriptSdkDir']
if (!adscriptSdkDir) {
    logger.warn("⚠️ WARNING: adscriptSdkDir not set.")
} else if (!rootProject.file(adscriptSdkDir).exists()) {
    logger.warn("⚠️ WARNING: adscriptSdkDir does not exist at: ${adscriptSdkDir}")
} else {
    def aarFile = new File(adscriptSdkDir, aarFileName)

    // The Adscript aar file is typically provided as .gz, check if we still need to extract.
    def gzFile = new File("${aarFile}.gz")
    if (!aarFile.exists() && gzFile.exists()) {
        new FileInputStream(gzFile).withCloseable { fis ->
            new GZIPInputStream(fis).withCloseable { gis ->
                new FileOutputStream(aarFile).withCloseable { fos ->
                    fos << gis
                }
            }
        }
    }
    dependencies {
        compileOnly fileTree(dir: "${rootProject.file(adscriptSdkDir)}/$aarFileName", include: ['*.aar', '*.jar'], exclude: [])
    }
}

dependencies {
    compileOnly "com.theoplayer.theoplayer-sdk-android:core:$sdkVersion"
    compileOnly "com.theoplayer.theoplayer-sdk-android:integration-ads-ima:$sdkVersion"
    implementation "com.google.android.gms:play-services-ads-identifier:17.0.1"
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
}