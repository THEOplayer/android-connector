apply plugin: 'maven-publish'

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
            // We use Dokka for JavaDoc generation, see dokkaJavadocJar below
            // withJavadocJar()
        }
    }
}

tasks.register("dokkaJavadocJar", Jar) {
    group = "documentation"
    dependsOn(tasks.dokkaJavadoc)
    from(tasks.dokkaJavadoc)
    archiveClassifier.set("javadoc")
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "reposilite"
                url = uri("https://reposilite.prudentgiraffe.com/releases")
                credentials {
                    username = System.getenv("REPOSILITE_USERNAME")
                    password = System.getenv("REPOSILITE_PASSWORD")
                }
            }
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/THEOplayer/android-connector")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                groupId project.ext.groupId
                artifactId project.name
                version project.ext.sdkVersion
                artifact(tasks.dokkaJavadocJar)
                from components.release
            }
        }
    }
}
